! Program : poly_to_fgong
! Purpose : convert a polytrope to FGONG format
!
! Copyright 2015-2024 Rich Townsend & The GYRE Team
!
! This file is part of GYRE. GYRE is free software: you can
! redistribute it and/or modify it under the terms of the GNU General
! Public License as published by the Free Software Foundation, version 3.
!
! GYRE is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'gyre.inc'

program poly_to_fgong

   ! Uses

   use forum_m, only: RD, arg_parser_t, OPT_NO_ARG

   use gyre_m
   use poly_file_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Parameters

   integer, parameter :: IVERS = 1300
   integer, parameter :: ICONST = 15
   integer, parameter :: IVAR = 40

   ! Variables

   character(:), allocatable :: in_file_name
   character(:), allocatable :: out_file_name
   logical                   :: drop_outer

   type(arg_parser_t)      :: arg_parser
   type(model_par_t)       :: ml_p
   class(model_t), pointer :: ml
   type(grid_t)            :: gr
   real(RD), allocatable   :: V_2(:)
   real(RD), allocatable   :: As(:)
   real(RD), allocatable   :: U(:)
   real(RD), allocatable   :: c_1(:)
   real(RD), allocatable   :: Gamma_1(:)
   real(RD), allocatable   :: M_r(:)
   real(RD), allocatable   :: P(:)
   real(RD), allocatable   :: rho(:)
   integer                 :: n
   real(RD), allocatable   :: glob(:)
   real(RD), allocatable   :: var(:,:)
   integer                 :: unit
   integer                 :: j

   ! Parse command-line arguments

   drop_outer = .FALSE.

   arg_parser = arg_parser_t('poly_to_fgong POLY_FILE FGONG_FILE', auto_help=.TRUE.)

   call arg_parser%define_option('drop-outer', OPT_NO_ARG, &
      usage='--drop-outer', description='drop outer point from model')

   call arg_parser%parse(arg_proc, opt_proc)

   if (.NOT. ALLOCATED(in_file_name)) call print_summary()
   if (.NOT. ALLOCATED(out_file_name)) call print_summary()

   ! Initialize

   call init_math()

   ! Read the polytrope data

   ml_p%file = TRIM(in_file_name)

   call read_poly_model(ml_p, ml)

   ! Set up the grid

   gr = ml%grid()

   if (drop_outer) then
      gr = grid_t(gr%pt(:gr%n-1)%x)
   endif

   ! Extract data from the model

   ! Dimensionless structure variables

   allocate(V_2(gr%n))
   allocate(As(gr%n))
   allocate(U(gr%n))
   allocate(c_1(gr%n))
   allocate(Gamma_1(gr%n))

   do j = 1, gr%n
      associate (pt => gr%pt(j))
         V_2(j) = ml%coeff(I_V_2, pt)
         As(j) = ml%coeff(I_AS, pt)
         U(j) = ml%coeff(I_U, pt)
         c_1(j) = ml%coeff(I_C_1, pt)
         Gamma_1(j) = ml%coeff(I_GAMMA_1, pt)
      end associate
   end do

   ! Physical structure variables

   M_r = M_SUN*(gr%pt%x**3/c_1)

   P = (G_GRAVITY*M_SUN**2/(4._RD*PI*R_SUN**4))* &
      (U/(c_1**2*V_2))

   rho = (M_SUN/(4._RD*PI*R_SUN**3))*(U/c_1)

   ! Store into var array

   n = gr%n

   allocate(glob(ICONST))
   allocate(var(IVAR,n))

   glob(1) = M_SUN
   glob(2) = R_SUN
   glob(3:) = 0._RD

   var(1,:) = gr%pt%x*R_SUN

   where (M_r /= 0._RD)
      var(2,:) = log(M_r/M_SUN)
   elsewhere
      var(2,:) = log(1E-38_RD)
   endwhere

   var(3,:) = 0._RD
   var(4,:) = P
   var(5,:) = rho
   var(6:9,:) = 0._RD
   var(10,:) = Gamma_1
   var(11:14,:) = 0._RD
   var(15,:) = As
   var(16,:) = 0._RD

   var = var(:,n:1:-1)

   ! Write out the FGONG file

   open(NEWUNIT=unit, FILE=out_file_name, STATUS='REPLACE')

   write(unit, 100) 'Fee'
   write(unit, 100) 'Fi'
   write(unit, 100) 'Fo'
   write(unit, 100) 'Fum'
100 format(A)

   write(unit, 110) n, ICONST, IVAR, IVERS
110 format(4I10)

   write(unit, 120) glob
120 format(1P,5(1X,E26.18E3))

   do j = 1, n
      write(unit, 120) var(:,j)
   end do

   close(unit)

   ! Finish

contains

   subroutine arg_proc(a, value)

      integer, intent(in)      :: a
      character(*), intent(in) :: value

      select case(a)
      case(1)
         in_file_name = value
      case(2)
         out_file_name = value
      case default
         call print_summary()
      end select

   end subroutine arg_proc

   !****

   subroutine opt_proc(name, value)

      character(*), intent(in) :: name
      character(*), intent(in) :: value

      integer :: stat

      stat = STAT_OK

      select case(name)
      case('drop-outer')
         drop_outer = .TRUE.
      case('help')
         call print_summary()
      case default
         @:ABORT('invalid option name')
      end select

      if (stat /= STAT_OK) then
         @:STOP('invalid --'//name)
      end if

   end subroutine opt_proc

   !****

   subroutine print_summary()

      call arg_parser%print_summary()
      @:STOP()

   end subroutine print_summary

end program poly_to_fgong
